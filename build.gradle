plugins {
    id 'java'
    id 'application'
    id "com.github.ben-manes.versions" version "0.53.0"

}

group = 'dev'
version = '1.0-SNAPSHOT'

repositories {
    mavenCentral()
}

application {
    mainClass.set('dev.Main')
}

dependencies {
    implementation 'com.h2database:h2:2.4.240'
    implementation 'org.jdbi:jdbi3-core:3.50.0'
    implementation 'org.jdbi:jdbi3-sqlobject:3.50.0'
    implementation 'org.slf4j:slf4j-simple:2.0.17'
    compileOnly 'org.projectlombok:lombok:1.18.42'
    annotationProcessor 'org.projectlombok:lombok:1.18.42'

    testCompileOnly 'org.projectlombok:lombok:1.18.42'
    testAnnotationProcessor 'org.projectlombok:lombok:1.18.42'
    testImplementation platform('org.junit:junit-bom:6.0.1')
    testImplementation 'org.junit.jupiter:junit-jupiter:6.0.1'
}

def isNonStable = { String version ->
    def stableKeyword = ['RELEASE', 'FINAL', 'GA'].any { it -> version.toUpperCase().contains(it) }
    def regex = /^[0-9,.v-]+(-r)?$/
    return !stableKeyword && !(version ==~ regex)
}

tasks.named('dependencyUpdates').configure {
    rejectVersionIf {
        isNonStable(it.candidate.version)
    }
}

tasks.withType(JavaExec) {
    systemProperties = ['org.slf4j.simpleLogger.defaultLogLevel': 'debug']
}


test {
    useJUnitPlatform()
}
