plugins {
    id 'java'
    id 'application'
    id "com.github.ben-manes.versions" version "0.52.0"

}

group = 'dev'
version = '1.0-SNAPSHOT'

repositories {
    mavenCentral()
}

application {
    mainClass.set('dev.Main')
}

dependencies {
    implementation 'com.h2database:h2:2.3.232'
    implementation 'org.jdbi:jdbi3-core:3.49.4'
    implementation 'org.jdbi:jdbi3-sqlobject:3.49.3'
    implementation 'org.slf4j:slf4j-simple:2.0.17'
    compileOnly 'org.projectlombok:lombok:1.18.38'
    annotationProcessor 'org.projectlombok:lombok:1.18.38'

    testCompileOnly 'org.projectlombok:lombok:1.18.38'
    testAnnotationProcessor 'org.projectlombok:lombok:1.18.38'
    testImplementation platform('org.junit:junit-bom:5.12.2')
    testImplementation 'org.junit.jupiter:junit-jupiter:5.12.2'
}

def isNonStable = { String version ->
    def stableKeyword = ['RELEASE', 'FINAL', 'GA'].any { it -> version.toUpperCase().contains(it) }
    def regex = /^[0-9,.v-]+(-r)?$/
    return !stableKeyword && !(version ==~ regex)
}

tasks.named('dependencyUpdates').configure {
    rejectVersionIf {
        isNonStable(it.candidate.version)
    }
}

tasks.withType(JavaExec) {
    systemProperties = ['org.slf4j.simpleLogger.defaultLogLevel': 'debug']
}


test {
    useJUnitPlatform()
}
